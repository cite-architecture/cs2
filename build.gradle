buildscript {
  repositories {
    jcenter()
    // enable this to use snapshot versions of Gretty:
    // maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
  }

  dependencies {
    classpath 'org.akhikhl.gretty:gretty:+'
  }
}

repositories {
  jcenter()
  // enable this to use snapshot versions of Gretty:
  // maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
}
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'


task configure {
  description = "Generates fuseki configuration file based on current project location."
  inputs.file "${rootProject.projectDir}/fuseki/config-template.ttl"
  outputs.file "${rootProject.projectDir}/fuseki/fusekibase/config.ttl"
  doLast {
    copy {
      from (file("${rootProject.projectDir}/fuseki")) {
        include 'config-template.ttl'
        rename 'config-template.ttl', 'config.ttl'
      }
      filter(ReplaceTokens, tokens : ["projectroot" : "${rootProject.rootDir}".toString() ] )
      into "${rootProject.projectDir}/fuseki/fusekibase"
    }
  }
}


allprojects {
  apply from: "${rootProject.rootDir}/versions.gradle"
  version = "1.0.0"
}


subprojects {
  apply plugin: "groovy"
  // plugins shared across subprojects:
  //apply from: "${rootProject.rootDir}/common/common.plugin"


  configurations.compile.transitive = true

  repositories {
    mavenCentral()
    maven {
      url "http://beta.hpcc.uh.edu/nexus/content/groups/public"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: versionMap['groovy']
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: versionMap['groovy']

    testCompile "org.concordion:concordion:${versionMap['concordion']}"
    testCompile "junit:junit:${versionMap['junit']}"

    compile group: 'xmlunit', name: 'xmlunit', version: '1.6'
    compile group: 'edu.harvard.chs', name: 'cite', version: versionMap['cite']
    compile group: 'edu.holycross.shot', name: 'prestochango', version: versionMap['prestochango']
    compile group: 'commons-io', name: 'commons-io', version: versionMap['commonsio']

    //testCompile group: 'com.github.rjeschke', name: 'txtmark', version: '0.11'

  }

  task initProject(description: 'Initializes project directories') << { task ->
    task.project.sourceSets*.groovy.srcDirs*.each {
      println "Create directory $it"
      it.mkdirs()
    }
  }

  task testCompPath () << { task ->

    println "\nFirst, all configs in ${version} in ${task.project.name}:"
    configurations.each {
      println it
    }
    println "\nFiles on test compile CP for ${version} in ${task.project.name}:"
    configurations.testCompile.each {
      println "testCompile: $it"
    }
  }
}

farms {
  farm 'suite', {
    webapp ":webapp"
    webapp ":sparqlcts"
    webapp ":sparqlcc"
    webapp "${rootDir}/fuseki/fuseki.war"
  }
  farm 'fuse', {
    webapp "${rootDir}/fuseki/fuseki.war"
  }

}

products {
  product 'suite', {
    webapp ":webapp"
    webapp ":sparqlcts"
    webapp ":sparqlcc"
    webapp "${rootDir}/fuseki/fuseki.war"
  }
}
