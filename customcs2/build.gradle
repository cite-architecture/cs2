import org.apache.tools.ant.filters.ReplaceTokens

if (hasProperty("custom")) {
  apply from: new File(custom)
} else {
  apply from: "customconf.gradle-template"
}


buildscript {
  repositories {
    jcenter()
    mavenCentral()
    maven {
      url "http://beta.hpcc.uh.edu/nexus/content/groups/public"
    }
  }

  dependencies {
    classpath 'org.akhikhl.gretty:gretty:+'
    classpath group: 'edu.harvard.chs', name: 'cite', version: versionMap['cite']
  }
}

apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'


repositories {
  jcenter()
  // enable this to use snapshot versions of Gretty:
  // maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
}


task installSource(type: Copy) {
  from(sourceDirectory)
  into ("src/main/webapp")
  filter(ReplaceTokens, tokens: cs2Tokens)
}

gretty {
  contextPath = cs2Tokens["contextPath"]

  initParameter("tripleserver", cs2Tokens["tripleserver"])
  initParameter("version",cs2Tokens["cs2version"])

  webappCopy {
    filesMatching '*', { FileCopyDetails fileDetails ->
      logger.warn 'File filtered: {}', fileDetails.path
      filter ReplaceTokens, tokens: cs2Tokens
    }
    filesMatching '**/*.xml', { FileCopyDetails fileDetails ->
      logger.warn 'File filtered: {}', fileDetails.path
      filter ReplaceTokens, tokens: cs2Tokens
    }
    filesMatching '**/*.xsl', { FileCopyDetails fileDetails ->
      logger.warn 'File filtered: {}', fileDetails.path
      filter ReplaceTokens, tokens: cs2Tokens
    }
  }
  overlay ':cs2'
}
