import org.apache.tools.ant.filters.ReplaceTokens

if (hasProperty('solo')) {
  apply from: "ctsconf.gradle"
} else if (hasProperty('custom')) {
  apply from: new File(custom)
} else {
  apply from: "../cs2/conf.gradle"
}


buildscript {
  repositories {
    jcenter()
    mavenCentral()
    maven {
      url "http://beta.hpcc.uh.edu/nexus/content/groups/public"
    }
  }

  dependencies {
    classpath 'org.akhikhl.gretty:gretty:+'
    classpath group: 'edu.harvard.chs', name: 'cite', version: versionMap['cite']
  }
}

apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'


repositories {
  jcenter()
  // enable this to use snapshot versions of Gretty:
  // maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

sourceSets {
  integr {
  }
}

dependencies {

  // Need to define all dependencies for integration tests:
  integrCompile "junit:junit:${versionMap['junit']}"
  integrCompile project(":sparqlcts")
  integrCompile  group: 'org.codehaus.groovy', name: 'groovy-all', version: versionMap['groovy']
}



farm {
    integrationTestTask = 'ctsIntegration'
    webapp "${rootDir}/fuseki/fuseki.war"
    webapp project(":sparqlimg")
	webapp project(":sparqlcc")
	webapp project(":sparqlcts")
}

test {
  include '**/Test*.*'
  include '**/*Test.*'
  exclude '**/*Integr.*'
}


task ctsIntegration(type: Test, dependsOn: 'test') {
  description  = "Runs integration tests against test data set served from fuseki."
  outputs.upToDateWhen { false }
  include '**/*Integr.*'

  testClassesDir = sourceSets.integr.output.classesDir
  classpath = sourceSets.integr.runtimeClasspath
}
ctsIntegration.doFirst {
  System.out.println "Running CTS integration tests now...."
}


gretty {
  initParameter("tripleserver", cs2Tokens['tripleserver'])
  initParameter("version",cs2Tokens['cs2version'])
  webappCopy {
    filesMatching '*', { FileCopyDetails fileDetails ->
      logger.warn 'File filtered: {}', fileDetails.path
      filter ReplaceTokens, tokens: cs2Tokens
    }
    filesMatching '**/*.xml', { FileCopyDetails fileDetails ->
      logger.warn 'File filtered: {}', fileDetails.path
      filter ReplaceTokens, tokens: cs2Tokens
    }
    filesMatching '**/*.xsl', { FileCopyDetails fileDetails ->
      logger.warn 'File filtered: {}', fileDetails.path
      filter ReplaceTokens, tokens: cs2Tokens
    }
  }
}
